group = 'es.antoniogo'
version = '0.0.1-SNAPSHOT'

allprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'

    sourceCompatibility = '11'

    repositories {
        mavenCentral()
    }

    dependencies {
        developmentOnly 'org.springframework.boot:spring-boot-devtools'

        testImplementation 'com.github.javafaker:javafaker:1.0.2'
    }

    test {
        useJUnitPlatform()
    }

    task view_paths {
        doLast { task ->
            println "$task.project.name"
            println "------------------"
            println "Main: $sourceSets.main.java.srcDirTrees"
            println "   Resources: $sourceSets.main.resources.srcDirTrees"
            println "Tests: $sourceSets.test.java.srcDirTrees"
            println "   Resources: $sourceSets.test.resources.srcDirTrees"
        }
    }

}

subprojects {
    group = "es.antoniogo.${rootProject.name}"

    sourceSets {
        main {
            java { srcDirs = ['src/main/java'] }
            resources { srcDirs = ['src/main/resources'] }
        }

        test {
            java { srcDirs = ['src/test/java'] }
            resources { srcDirs = ['src/test/resources'] }
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.hibernate:hibernate-core:5.5.2.Final'
        implementation 'org.springframework:spring-orm'
        implementation 'com.zaxxer:HikariCP:4.0.3'

        testImplementation rootProject.sourceSets.main.output
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        runtimeOnly 'mysql:mysql-connector-java:8.0.25'

        if (project.name != "shared") {
            implementation project(":shared")
            testImplementation project(":shared").sourceSets.test.output
        }
    }

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.5.1")
    }
}

sourceSets {
    main {
        java { srcDirs = ['apps/src/main/java'] }
        resources { srcDirs = ['apps/src/main/resources'] }
    }

    test {
        java { srcDirs = ['apps/src/test/java'] }
        resources { srcDirs = ['apps/src/test/resources'] }
    }
}

apply plugin: 'application'

bootJar {

}

mainClassName = 'es.antoniogo.apps.Starter'

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"

    implementation project(":shared")
    implementation project(":management")

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation project(":shared").sourceSets.test.output
}
